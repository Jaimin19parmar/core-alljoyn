# # 
#    Copyright (c) 2016 Open Connectivity Foundation and AllJoyn Open
#    Source Project Contributors and others.
#    
#    All rights reserved. This program and the accompanying materials are
#    made available under the terms of the Apache License, Version 2.0
#    which accompanies this distribution, and is available at
#    http://www.apache.org/licenses/LICENSE-2.0

#

import os
import sys
sys.path.append('../../build_core/tools/scons')
from javastatus import JavaStatus
Import('env')

# AllJoyn Java status codes
JavaStatus('org/alljoyn/bus/Status')


# AllJoyn Java binding
if env['OS'] == 'android':
    main_classes = env.Java('$CLASSDIR', source = ['.'], JAVACLASSPATH = env.subst('$JAVACLASSPATH'))
    alljoyn_javah = env.JavaH(target = File('../jni/alljoyn_java.h'), source = [main_classes])

    if env.get('ANDROID_SDK', '') == '':
        print 'ANDROID_SDK must be set'
        if not GetOption('help'):
            Exit(1)

    env.Append(JAVACLASSPATH = os.pathsep + env.subst('$ANDROID_SDK/platforms/android-$ANDROID_API_LEVEL/android.jar'))
    main_classes += env.Java('$CLASSDIR', source = ['../helper/org/alljoyn/bus/alljoyn/DaemonInit.java'], JAVACLASSPATH = env.subst('$JAVACLASSPATH'))

    alljoyn_jar = env.Jar('$JARDIR/alljoyn.jar', source = [main_classes])

elif (env['OS'] == 'linux' or env['OS_GROUP'] == 'windows' or env['OS'] == 'darwin'):
    main_classes = env.Java('$CLASSDIR', source = ['.'], JAVACLASSPATH = env.subst('$JAVACLASSPATH'))
    alljoyn_jar = env.Jar('$JARDIR/alljoyn.jar', source = [main_classes])
    alljoyn_javah = env.JavaH(target = File('../jni/alljoyn_java.h'), source = [main_classes])

if env['VARIANT'] == 'debug' :
    # get a listing of the sources
    env.Jar('$JARDIR/alljoyn-sources.jar', source = ['.'])
Return('alljoyn_jar')