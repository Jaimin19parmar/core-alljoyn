# # 
#    Copyright (c) 2016 Open Connectivity Foundation and AllJoyn Open
#    Source Project Contributors and others.
#    
#    All rights reserved. This program and the accompanying materials are
#    made available under the terms of the Apache License, Version 2.0
#    which accompanies this distribution, and is available at
#    http://www.apache.org/licenses/LICENSE-2.0


Import('about_env')

if not about_env.has_key('_ALLJOYNCORE_'):
    about_env.Append(LIBS = ['alljoyn'])
    if about_env['BR'] == 'on' :
        about_env.Prepend(LIBS = ['ajrouter'])
    if about_env['OS'] == 'openwrt':
        about_env.AppendUnique(LIBS = [ 'stdc++', 'pthread' ])

# Make about dist a sub-directory of the alljoyn dist.  This avoids any conflicts with alljoyn dist targets.
about_env['ABOUT_DISTDIR'] = about_env['DISTDIR'] + '/cpp'

about_env.Install('$ABOUT_DISTDIR/inc/alljoyn/about', about_env.Glob('inc/alljoyn/about/*.h'))

# About libraries
about_env.Install('$ABOUT_DISTDIR/lib', about_env.SConscript('src/SConscript', exports = ['about_env']))

# Sample programs
about_env.Install('$ABOUT_DISTDIR/bin/samples', about_env.SConscript('samples/SConscript', exports = ['about_env']))

# only include command line samples, unit test, test programs if we are not
# building for iOS. No support on iOS for command line applications.
if not ((about_env['OS'] == 'darwin' and about_env['CPU'] in ['arm', 'armv7', 'armv7s', 'arm64']) or about_env['OS'] == 'iOS'):
    # Build unit Tests
    about_env.SConscript('unit_test/SConscript', exports = ['about_env'], variant_dir='$OBJDIR_ABOUT/unittest', duplicate = 0)