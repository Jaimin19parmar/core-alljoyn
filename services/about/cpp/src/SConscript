# # 
#    Copyright (c) 2016 Open Connectivity Foundation and AllJoyn Open
#    Source Project Contributors and others.
#    
#    All rights reserved. This program and the accompanying materials are
#    made available under the terms of the Apache License, Version 2.0
#    which accompanies this distribution, and is available at
#    http://www.apache.org/licenses/LICENSE-2.0


Import('about_env')
import os

# even though we have deprecated the About Service code we still want to build
# the code till the code is ready for removal.
if about_env['OS_GROUP'] == 'posix':
    about_env.Append(CXXFLAGS = ['-Wno-deprecated-declarations'])
if about_env['OS_GROUP'] == 'windows':
    about_env.Append(CXXFLAGS = ['/wd4996'])

# Sources
srcs = about_env.Glob('*.cc')

# Platform specific sources
if about_env['OS'] == 'android':
    srcs += about_env.Glob('$OS/*.cc')
else:
    srcs += about_env.Glob('$OS_GROUP/*.cc')

libs = []

# Static library
objs = about_env.Object(srcs)
libs.append(about_env.StaticLibrary('alljoyn_about', objs))

# Shared library
if about_env.get('LIBTYPE', 'static') != 'static':
    shobjs = about_env.SharedObject(srcs)
    libs.append(about_env.SharedLibrary('alljoyn_about', shobjs))

Return ('libs')