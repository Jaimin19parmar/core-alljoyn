# # 
#    Copyright (c) 2016 Open Connectivity Foundation and AllJoyn Open
#    Source Project Contributors and others.
#    
#    All rights reserved. This program and the accompanying materials are
#    made available under the terms of the Apache License, Version 2.0
#    which accompanies this distribution, and is available at
#    http://www.apache.org/licenses/LICENSE-2.0

#

Import('ajenv')
Import('common_static_objs')
Import('common_shared_objs')
from os.path import basename

# Need to clone the environment so that building a shared library does not
# cause a circular dependency.
libenv = ajenv.Clone()

# AllJoyn Sources
srcs = [ f for f in libenv.Glob('*.cc') + libenv.Glob('*.c')
         if not (str(f).endswith('version.cc')) ]

# Platform specific sources
if libenv['OS_SUBGROUP'] == 'darwin':
    os_srcs = [ f for f in libenv.Glob(libenv['OS_SUBGROUP'] + '/*.cc') ]
    os_srcs += [ f for f in libenv.Glob(libenv['OS_GROUP'] + '/KeyStoreListenerFactory.cc') ]
else:
    os_srcs = [ f for f in libenv.Glob(libenv['OS_GROUP'] + '/*.cc') ]

version_cc = libenv.GenVersion('version')

static_objs = common_static_objs + libenv.Object(srcs) + libenv.Object(os_srcs)
libenv.Depends(version_cc, static_objs)
static_objs.append(libenv.Object(version_cc))

if libenv['LIBTYPE'] == 'static':
    shared_objs = []
else:
    shared_objs = common_shared_objs + libenv.SharedObject(srcs) + libenv.SharedObject(os_srcs)
    libenv.Depends(version_cc, shared_objs)
    shared_objs.append(libenv.SharedObject(version_cc))

    #Add hardening flags for linux release build only.
    #The goal is to avoid any impact for these flags to our capability of debugging.
    if (libenv['OS'] == 'linux') and (libenv['VARIANT'] == 'release'):
        libenv.Append(LINKFLAGS = ['-fPIC', '-Wl,-z,relro,-z,now'])

# AllJoyn Static Library
libs = libenv.StaticLibrary('alljoyn', static_objs)
if libenv['LIBTYPE'] != 'static':
    libs.append(libenv.SharedLibrary('alljoyn', shared_objs))

Return ('libs', 'static_objs', 'shared_objs')